buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.net.minecrell:licenser:0.3'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'net.minecrell.licenser'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    group = 'org.cadixdev'
    archivesBaseName = project.name.toLowerCase()
    version = '0.5.6-SNAPSHOT'

    repositories {
        mavenCentral()
        if (bombeVersion.endsWith("-SNAPSHOT")) {
            maven {
                url 'https://oss.sonatype.org/content/groups/public/'
            }
        }
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    }

    test {
        useJUnitPlatform()
    }

    license {
        header = rootProject.file('HEADER.txt')
    }

    processResources {
        from 'LICENSE.txt'
    }

    javadoc {
        def javadocArgsFile = project.file("${project.buildDir}/tmp/javadoc-tags.options")
        doFirst {
            javadocArgsFile.parentFile.mkdirs()
            // These tags aren't supported by default, though they are standard in the JDK
            // (and most tools, including IntelliJ, understand them by default)
            javadocArgsFile.write("""-tag "apiNote:a:API Note:"
-tag "implSpec:a:Implementation Requirements:"
-tag "implNote:a:Implementation Note:"
""")
        }
        options.optionFiles(javadocArgsFile)
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
        apply plugin: 'signing'
        signing {
            required { !version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(tasks.uploadArchives) }
            sign configurations.archives
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                // Maven Central
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                }

                pom {
                    artifactId = project.archivesBaseName

                    project {
                        name = project.name
                        description = project.description
                        packaging = 'jar'
                        url = project.url
                        inceptionYear = project.inceptionYear

                        scm {
                            url = 'https://github.com/CadixDev/Lorenz'
                            connection = 'scm:git:https://github.com/CadixDev/Lorenz.git'
                            developerConnection = 'scm:git:git@github.com:CadixDev/Lorenz.git'
                        }

                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/CadixDev/Lorenz/issues'
                        }

                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://opensource.org/licenses/MIT'
                                distribution = 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'jamierocks'
                                name = 'Jamie Mansfield'
                                email = 'jmansfield@cadixdev.org'
                                url = 'https://www.jamiemansfield.me/'
                                timezone = 'Europe/London'
                            }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}
